apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// For change release apk-file name and version
apply from: '../versioning.gradle'
apply from: "../artifacts.gradle"

def openProperties(fileName) {
    Properties properties = new Properties()
    if (project.rootProject.file(fileName).exists())
        properties.load(project.rootProject.file(fileName).newDataInputStream())
    return properties
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.rsastack"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode buildVersionCode()
        versionName version

        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }

    signingConfigs {

        release {
            storeFile file("../sign/release.jks")

            Properties prop = openProperties("sign/release.properties")
            storePassword prop.getProperty("keystorePassword")
            keyAlias prop.getProperty("keyAlias")
            keyPassword prop.getProperty("keyPassword")
        }

        debug {
            storeFile file('../sign/debug.jks')

            Properties prop = openProperties("sign/debug.properties")
            storePassword prop.getProperty("keystorePassword")
            keyAlias prop.getProperty("keyAlias")
            keyPassword prop.getProperty("keyPassword")
        }

    }

    flavorDimensions "default"

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }

        staging {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".staging"
            useProguard true
            // but in proguard-rules-dontobfuscate.pro add '-dontobfuscate'
            // see https://stackoverflow.com/questions/47893266/build-failing-on-play-services11-8-x-with-pro-guard-parser-error
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-dontobfuscate.pro'
        }

        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {

    implementation project(':rsastack')
    implementation project(':uicore')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // Support
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    //    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    //    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    // Moxy - lib for MVP architect (Used the custom moxy build with fixed bug with custom view)
    //implementation fileTree(include: ['moxy*.jar'], excludes: ['moxy-compiler*.jar'], dir: 'libs')
    compileOnly files('libs/moxy-1.5.6.jar')
    kapt files('libs/moxy-compiler-1.5.6.jar')
    // Cicerone (Navigation pattern)
    implementation 'ru.terrakok.cicerone:cicerone:5.0.0'
    // Toothpick
    implementation 'com.github.stephanenicolas.toothpick:toothpick-runtime:3.0.2'
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:3.0.2"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.6.1"
    implementation "com.squareup.retrofit2:converter-gson:2.6.1"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.6.1"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"
    implementation "ru.gildor.coroutines:kotlin-coroutines-retrofit:1.1.0"
    // okhttp3 (TODO: Migration to v4 https://square.github.io/okhttp/upgrading_to_okhttp_4/)
    implementation 'com.squareup.okhttp3:okhttp:3.14.4'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.4'
    // ToggleButtonLayout for stuff screen
    implementation 'com.github.savvyapps:ToggleButtonLayout:1.2.0'
    // Adapter simplify
    implementation('com.hannesdorfmann:adapterdelegates3:3.0.1') { transitive = false }
    // Junit
    testImplementation 'junit:junit:4.12'
    // Core library
    androidTestImplementation 'androidx.test:core:1.2.0'
    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    // Assertion simplify
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    androidTestImplementation 'com.google.truth:truth:1.0'
    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
}

kotlin {
    androidExtensions {
        experimental = true
    }
}


